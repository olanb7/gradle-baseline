apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply from: "${rootDir}/gradle/publish-libs.gradle"
apply from: "${rootDir}/gradle/java.gradle"

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile 'net.ltgt.gradle:gradle-errorprone-plugin'
    compile 'com.google.guava:guava'
    compile 'gradle.plugin.com.github.spotbugs:gradlePlugin:1.6.0'
    testCompile gradleTestKit()
    testCompile 'com.netflix.nebula:nebula-test' // for better temp directory junit rule only
    testCompile 'net.lingala.zip4j:zip4j'
    testCompile 'org.apache.commons:commons-io'
}

// Create version file included in jar so that the plugin can know about its own version
task createVersionFile {
    def versionFile = file("src/main/resources/baseline-version.txt")
    versionFile.parentFile.mkdirs()
    versionFile.write(project.version, "UTF-8")
}
compileGroovy.dependsOn createVersionFile

apply plugin: "com.gradle.plugin-publish"
pluginBundle {
    website = 'https://ghe.iparadigms.com/NCL-Hack-2018/gradle-baseline'
    vcsUrl = 'https://ghe.iparadigms.com/NCL-Hack-2018/gradle-baseline'
    description = 'Baseline Java is a collection of Gradle plugins for configuring code quality tools in builds and generated Eclipse/IntelliJ projects.'
    tags = ['java', 'checkstyle', 'code quality', 'eclipse', 'idea']

    plugins {
        baselinePlugins {
            id = 'com.turnitin.baseline'
            displayName = 'Turnitin Baseline Plugins'
        }
        baselineCheckstylePlugin {
            id = 'com.turnitin.baseline-checkstyle'
            displayName = 'Turnitin Baseline Checkstyle Plugin'
        }
        baselineConfigPlugin {
            id = 'com.turnitin.baseline-config'
            displayName = 'Turnitin Baseline Configuration Plugin'
        }
        baselineEclipsePlugin {
            id = 'com.turnitin.baseline-eclipse'
            displayName = 'Turnitin Baseline Eclipse Plugin'
        }
        baselineFindBugsPlugin {
            id = 'com.turnitin.baseline-findbugs'
            displayName = 'Turnitin Baseline Findbugs Plugin'
        }
        baselineIdeaPlugin {
            id = 'com.turnitin.baseline-idea'
            displayName = 'Turnitin Baseline IntelliJ Plugin'
        }
        baselinePmdPlugin {
            id = 'com.turnitin.baseline-pmd'
            displayName = 'Turnitin Baseline Pmd Plugin'
        }
        baselineClassUniquenessPlugin {
            id = 'com.turnitin.baseline-class-uniqueness'
            displayName = 'Turnitin Baseline Class Uniqueness Plugin'
        }
    }
}

